logger.lifecycle 'Enabling lint support in LensKit build'

ext.lintMode = 'disabled'
if (travis.isReleaseBuild()) {
    logger.lifecycle 'Travis build on release branch'
    ext.lintMode = 'enabled'
}
switch (getConfigProperty('lint', 'false').toLowerCase()) {
    case 'true':
        logger.info 'running in lint mode'
        ext.lintMode = 'enabled'
        break
    case 'preview':
        logger.info 'running in lint preview mode'
        ext.lintMode = 'preview'
        break
}

allprojects {
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = '0.7.7.201606060606'
    }
}

subprojects {
    tasks.withType(Test).all { t ->
        t.jacoco {
            enabled = lintMode != 'disabled'
        }
    }
    tasks.withType(JacocoReport).all { rpt ->
        rpt.reports {
            xml.enabled = true
        }
    }
}

task jacocoMergedReport(type: JacocoReport) {
    ext.projects = []
    onlyIf {
        lintMode != 'disabled'
    }
    for (prj in subprojects) {
        if (prj.name == 'lenskit-integration-tests') continue;
        projects << prj
        dependsOn prj.test
        sourceSets prj.sourceSets.test
        if (prj.sourceSets.test.allSource.files.isEmpty()) {
            logger.info '{} has no tests', prj.name
        } else {
            logger.info 'aggregating tests from {}', prj.name
            executionData prj.test
        }
    }
    reports {
        csv.enabled = false
        xml.enabled = true
        html.destination = "$reporting.baseDir/jacoco"
    }
}
report.dependsOn jacocoMergedReport
ciTest.dependsOn report

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoMergedReport/jacocoMergedReport.xml"
    sourceDirs = subprojects.collect({ prj ->
        if (prj.name == 'lenskit-integration-tests') {
            return []
        } else {
            return prj.sourceSets.main.java.srcDirs
        }
    }).flatten()
}

tasks.coveralls {
    group = 'report'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoMergedReport
    onlyIf { System.env.'CI' && travis.activeJdk == 'oraclejdk8' }
}
ciTest.dependsOn tasks.coveralls

sonarqube {
    properties {
        def branch = travis.branch
        def m = branch != null ? branch =~ /release\/(.*)/ : null
        if (m) {
            def bk = m.group(1)
            property "sonar.projectKey", "$project.group:$project.name-$bk"
        }
        property "sonar.host.url", "https://sonar.cs.txstate.edu"
        property "sonar.sourceEncoding", "UTF-8"
        if (System.getenv("SONAR_USER")) {
            logger.lifecycle("using Sonar user {}", System.getenv("SONAR_USER"))
            property "sonar.login", System.getenv("SONAR_USER")
            property "sonar.password", System.getenv("SONAR_PASSWORD")
        }
        if (lintMode == 'preview') {
            logger.info 'configuring Sonar for preview mode'
            property "sonar.analysis.mode", "preview"
            property "sonar.issuesReport.html.enable", "true"
        }
    }
}

if (lintMode == 'enabled') {
    ciPublish.dependsOn 'sonarqube'
}
